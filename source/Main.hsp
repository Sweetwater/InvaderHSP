#include "./source/Define.hsp"
#include "./source/Game.hsp"

#deffunc initializeSystem
	screen mainWindowID, screenWidth, screenHeight,,22,75
	randomize
	return

#deffunc initializeInput
	dim inputCode, 32
	inputCode(keyLeft) = 37
	inputCode(keyRight) = 39
	inputCode(keyUp) = 38
	inputCode(keyDown) = 40
	inputCode(keySpace) = 32
	inputCode(keyEsc) = 27
	inputCode(keyShift) = 16
	inputCode(keyW) = 87
	inputCode(keyA) = 65
	inputCode(keyS) = 83
	inputCode(keyD) = 68
	inputCode(keyD0) = 48
	inputCode(keyD1) = 49
	inputCode(keyD2) = 50
	inputCode(keyD3) = 51
	inputCode(keyD4) = 52
	inputCode(keyD5) = 53
	inputCode(keyD6) = 54
	inputCode(keyD7) = 55
	inputCode(keyD8) = 56
	inputCode(keyD9) = 57
	inputCode(mouseLeft) = 1
	inputCode(mouseRight) = 2

	gOldKeyState = 0
	gNewKeyState = 0
	gTriggerKeyState = 0
	gReleaseKeyState = 0

	gOldMouseX = mousex
	gNewMouseX = mousex
	gMouseMoveX = mousex

	gOldMouseY = mousey
	gNewMouseY = mousey
	gMouseMoveY = mousey

	return

#deffunc updateInput
	gOldKeyState = gNewKeyState
	gNewKeyState = 0
	repeat length(inputCode)
		getkey inputKey, inputCode(cnt)
		if inputKey : gNewKeyState |= (1 << cnt)
	loop

	gTriggerKeyState = (gOldKeyState ^ $FFFFFFFF) & gNewKeyState
	gReleaseKeyState = (gNewKeyState ^ $FFFFFFFF) & gOldKeyState

	gOldMouseX = gNewMouseX
	gNewMouseX = mousex
	gMouseMoveX = gNewMouseX - gOldMouseX

	gOldMouseY = gNewMouseY
	gNewMouseY = mousey
	gMouseMoveY = gNewMouseY - gOldMouseY

	return

#defcfunc isKeyUp int key
	return isKeyDown(key) == false

#defcfunc isKeyDown int key
	return (gNewKeyState >> key) & 0x00000001

#defcfunc isKeyTrigger int key
	return (gTriggerKeyState >> key) & 0x00000001

#defcfunc isKeyRelease int key
	return (gReleaseKeyState >> key) & 0x00000001

#defcfunc getMouseX
	return gNewMouseX

#defcfunc getMouseY
	return gNewMouseY

#defcfunc getMouseMoveX
	return gMouseMoveX

#defcfunc getMouseMoveY
	return gMouseMoveY




*main
	initializeSystem
	initializeInput

	loadGame
	initializeGame

	repeat
		redraw 0
//		redraw 1
		color backgoundColor
		boxf 0, 0, screenWidth, screenHeight

		updateInput
		updateGame
		drawGame

		if isKeyDown(keyEsc) {
			goto *gameEnd
		}

		redraw 1
		await FPS
	loop

*gameEnd
	end
