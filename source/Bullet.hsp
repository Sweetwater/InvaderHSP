#ifndef ___Bullet___
#define ___Bullet___


#include "./source/Define.hsp"

#module ModBullet mX, mY, mDirection, mIsActive, mImageID

#const moveY 4

#const yLimitMin 0
#const yLimitMax screenHeight

#modinit int imageID
	mImageID = imageID
	mIsActive = false
	return

#modterm
	return

#modfunc bulletUpdate
	if mIsActive == false : return

	if mDirection == dirUp:mY -= moveY
	if mDirection == dirDown:mY += moveY

	mY = limit(mY, yLimitMin, yLimitMax)
	if (mY = yLimitMin) | (mY = yLimitMax) {
		mIsActive = false
	}
	return

#modfunc bulletDraw
	if mIsActive == false : return
	pos mX, mY
	celput mImageID, 0
	return



#modcfunc bulletGetX
	return mX

#modcfunc bulletGetY
	return mY

#modcfunc bulletIsActive
	return mIsActive

#modfunc bulletFire int x, int y, int direction
	if mIsActive : return
	mX = x
	mY = y
	mDirection = direction
	mIsActive = true
	return

#modfunc bulletDisable
	mIsActive = false
	return

#global


#endif
