#ifndef ___Game___
#define ___Game___

#include "./source/Define.hsp"
#include "./source/Player.hsp"
#include "./source/Bunker.hsp"
#include "./source/Rocket.hsp"
#include "./source/RocketBullet.hsp"
#include "./source/Apache.hsp"
#include "./source/ApacheContoller.hsp"


#module Game

#enum stateInitialize = 0
#enum statePlay

#deffunc loadGame
	celload "./data/nico.bmp"
	gMyShipImageID = stat
	celdiv gMyShipImageID, 32,32, 16,16

	celload "./data/apache.bmp"
	gApacheImageID = stat
	celdiv gApacheImageID, 32,75, 17,39

	celload "./data/cannonShell.bmp"
	gCShellImageID = stat
	celdiv gCShellImageID, 16,16, 8,8

	celload "./data/rocketBullet.bmp"
	gRBulletImageID = stat
	celdiv gRBulletImageID, 28,9, 17,4

	celload "./data/rpg7.bmp"
	gRocketImageID = stat
	celdiv gRocketImageID, 42,9, 10,4

	celload "./data/bunker.bmp"
	gBunkerImageID = stat
	celdiv gBunkerImageID, 50,50, 25,25

	celload "./data/background.bmp"
	gBackgroundImageID = stat
	celdiv gBackgroundImageID, 320,240, 0,0

	gBackBufferID = gBackgroundImageID + 1
	buffer gBackBufferID, 64,64
	celdiv gBackBufferID, 64,64, 32,32

	return


#deffunc initializeGame
// TODO : モジュール変数の多次元配列の作成方法を調べる
//	dimtype gEnemys, vartype("struct"), 4,10

	repeat apacheMaxNum
		newmod gApaches, ModApache, gApacheImageID, gRBulletImageID, gCShellImageID, gBackBufferID
	loop
	repeat apacheMaxNum
		newmod gACtrs, ModApacheController, gApaches(cnt)
	loop

	repeat bunkerMaxNum
		newmod gBunkers, ModBunker, gBunkerImageID
	loop
	
	repeat playerBulletMaxNum
		newmod gPlayerBullets, ModRocketBullet, gRBulletImageID, gBackBufferID
	loop
	repeat rocketMaxNum
		newmod gRockets, ModRocket, gRocketImageID, gPlayerBullets(cnt)
	loop
	newmod gPlayer, ModPlayer, gMyShipImageID

// TODO : 引数で渡した配列の保持の方法を調べる
//	playerSetBullets player, bullets

	state = stateInitialize

	return

#deffunc resetBunkers array _bunkers
	repeat length(_bunkers)
		x = cnt * bunkerOffsetX + bunkerStartX
		y = bunkerStartY
		bunkerReset _bunkers(cnt), x, y
	loop
	return


#deffunc updateInitState
	resetBunkers gBunkers
	rocketRespawnCounter = 0
	apacheRespawnCounter = 0
	state = statePlay
	return

#deffunc updateRocketRespawn
	if rocketRespawnCounter != 0 {
		rocketRespawnCounter--
	}
	else {
		repeat length(gRockets)
			if rocketIsDisable(gRockets(cnt)) {
				x = rnd(screenWidth)
				rocketRespawn gRockets(cnt), x
				rocketRespawnCounter = rocketRespawnCount
				break
			}
		loop
	}
	return

#deffunc updateApacheRespawn
	if apacheRespawnCounter != 0 {
		apacheRespawnCounter--
	}
	else {
		repeat length(gApaches)
			if apacheIsDisable(gApaches(cnt)) {
				aCtrRespawn gACtrs(cnt)
				apacheRespawnCounter = apacheRespawnCount
				break
			}
		loop
	}
	return


#deffunc updatePlayState
	updateRocketRespawn
	updateApacheRespawn

	playerUpdate gPlayer

	repeat length(gRockets)
		rocketUpdate gRockets(cnt)
	loop

	repeat length(gApaches)
		aCtrUpdate gACtrs(cnt)
		apacheUpdate gApaches(cnt)
	loop

	repeat length(gPlayerBullets)
		rBulletUpdate gPlayerBullets(cnt)
	loop

	repeat length(gBunkers)
		bunkerUpdate gBunkers(cnt)
	loop

	checkHitRBulletApache gPlayerBullets
	checkHitRBulletBunker gPlayerBullets
	return


#deffunc updateGame

	if state == stateInitialize {
		updateInitState
		return
	}
	if state == statePlay {
		updatePlayState
		return
	}

	return


#deffunc drawGame

	gsel 0
	gmode 1
	pos 0,0
	celput gBackgroundImageID, 0, 2, 2

	gmode 4,,,255

	repeat length(gRockets)
		rocketDraw gRockets(cnt)
	loop

	playerDraw gPlayer

	repeat length(gApaches)
		apacheDraw gApaches(cnt)
	loop

	repeat length(gPlayerBullets)
		rBulletDraw gPlayerBullets(cnt)
	loop

	repeat length(gBunkers)
		bunkerDraw gBunkers(cnt)
	loop

	return



#deffunc getPickupRocket var _player, int _x, int _y
	repeat length(gRockets)
		if rocketIsReady(gRockets(cnt)) == false: continue

		x = rocketGetX(gRockets(cnt))
		y = rocketGetX(gRockets(cnt))
		diffX = x - _x
		diffY = y - _y
		if abs(diffX) < pickupRageX & abs(diffX) < pickupRageY {
			// モジュール変数型を返せないので
			// こちらからプレイヤーのメソッドを呼び出す
			rocket = gRockets(cnt)
			playerSetRocket _player, rocket
			break
		}
	loop
	return



#deffunc checkHitCShellPlaer array _cShells

	if playerIsDisable(gPlayer) : return

	pX = playerGetX(gPlayer)
	pY = playerGetY(gPlayer)

	hitIndex = checkHitCShells(_cShells, pX,pY, playerRangeX,playerRangeY)
	if hitIndex != -1 {
	   playerDisable gPlayer
	   cShellDisable _cShells(hitIndex)
	}
	return


#deffunc checkHitCShellBunker array _cShells
	repeat length(gBunkers)
		i = cnt
		if bunkerIsDisable(gBunkers(i)) : continue

		bX = bunkerGetX(gBunkers(i))
		bY = bunkerGetY(gBunkers(i))

		hitIndex = checkHitCShells(_cShells, bX,bY, bunkerRangeX,bunkerRangeY)
		if hitIndex != -1 {
		   bunkerDamage gBunkers(i), cShellDamage
		   cShellDisable _cShells(hitIndex)
		}
	loop
	return


#defcfunc checkHitCShells	array _cShells,				\
							int _x, int _y,				\
							int _rangeX, int _rangeY,	\
							local _i

	hitIndex = -1
	rangeX = sShellRangeX + _rangeX
	rangeY = sShellRangeY + _rangeY
	repeat length(_cShells)
		_i = cnt
		if cShellIsDisable(_cShells(_i)) : continue

		cX = cShellGetX(_cShells(_i))
		cY = cShellGetY(_cShells(_i))

		if isHit(_x,_y, cX,cY, rangeX,rangeY) {
			hitIndex = _i
			break
		}
	loop
	return hitIndex




#deffunc checkHitRBulletApache array _rBullets
	repeat length(gApaches)
		i = cnt
		if apacheIsDisable(gApaches(i)) : continue

		aX = apacheGetX(gApaches(i))
		aY = apacheGetY(gApaches(i))

		hitIndex = checkHitRBullets(_rBullets, aX,aY, apacheRangeX,apacheRangeY)
		if hitIndex != -1 {
		   apacheDisable gApaches(i)
		   rBulletDisable _rBullets(hitIndex)
		}
	loop
	return


#deffunc checkHitRBulletPlaer array _rBullets

	if playerIsDisable(gPlayer) : return

	pX = playerGetX(gPlayer)
	pY = playerGetY(gPlayer)

	hitIndex = checkHitCShells(_rBullets, pX,pY, playerRangeX,playerRangeY)
	if hitIndex != -1 {
	   playerDisable gPlayer
	   rBulletDisable _rBullets(hitIndex)
	}
	return


#deffunc checkHitRBulletBunker array _rBullets
	repeat length(gBunkers)
		i = cnt
		if bunkerIsDisable(gBunkers(i)) : continue

		bX = bunkerGetX(gBunkers(i))
		bY = bunkerGetY(gBunkers(i))

		hitIndex = checkHitRBullets(_rBullets, bX,bY, bunkerRangeX,bunkerRangeY)
		if hitIndex != -1 {
		   bunkerDamage gBunkers(i), rBulletDamage
		   rBulletDisable _rBullets(hitIndex)
		}
	loop
	return


#defcfunc checkHitRBullets	array _rBullets,				\
							int _x, int _y,				\
							int _rangeX, int _rangeY,	\
							local _i

	hitIndex = -1
	rangeX = rBulletRangeX + _rangeX
	rangeY = rBulletRangeY + _rangeY
	repeat length(_rBullets)
		_i = cnt
		if rBulletIsDisable(_rBullets(_i)) : continue

		rX = rBulletGetX(_rBullets(_i))
		rY = rBulletGetY(_rBullets(_i))

		if isHit(_x,_y, rX,rY, rangeX,rangeY) {
			hitIndex = _i
			break
		}
	loop
	return hitIndex




#defcfunc isHit double _x1, double _y1,	\
				double _x2, double _y2,	\
				double _rangeX, double _rangeY

	diffX = absf(_x1 - _x2)
	diffY = absf(_y1 - _y2)

	isHitX = diffX < _rangeX
	isHitY = diffY < _rangeY

	return isHitX & isHitY


#global

#endif
