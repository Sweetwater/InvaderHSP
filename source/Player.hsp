#ifndef ___Player___
#define ___Player___

#include "./source/Define.hsp"
#include "./source/Bullet.hsp"
#include "./source/Rocket.hsp"
#include "./source/Animation.hsp"


#module ModPlayer	mX, mY,				\
					mMoveX, mMoveY,		\
					mDirection,			\
					mEscKeyCounterL,	\
					mEscKeyCounterR,	\
					mAction,			\
					mActionSub,			\
					mActionCounter,		\
					mAngle,				\
					mIsOldActionChange,	\
					mIsActionChange,	\
					mIsActive,			\
					mAnim,				\
					mRocket,			\
					mImageID

#include "hspmath.as"

#const startX screenCenterX
#const baseY 450

#const walkX 1

#const escKeyCount 10

#const bodySize 32
#const xLimitMin 0 + bodySize/2
#const xLimitMax screenWidth - bodySize/2

#enum actionStop = 0
#enum actionWalk
#enum actionSprint
#enum actionCreeping
#enum actionEscape
#enum actionAIM

#enum aStL = 0
#enum aStR
#enum aWlL
#enum aWlR
#enum aSpReadyL
#enum aSpSprintL
#enum aSpSlipL
#enum aSpReadyR
#enum aSpSprintR
#enum aSpSlipR
#enum aEsJumpL
#enum aEsStopL
#enum aEsJumpR
#enum aEsStopR
#enum aCrL
#enum aCrR
#enum aAiR0
#enum aAiR1
#enum aAiR2
#enum aAiU
#enum aAiL2
#enum aAiL1
#enum aAiL0


#modinit int _imageID
	mImageID = _imageID
	mX = startX
	mY = baseY
	mMoveX = 0
	mMoveY = 0
	mAngle = 0
	mDirection = dirLeft
	mAction = -1
	mIsActive = true

	playerInitAnimData thismod
	newmod mAnim, ModAnimation

	playerChangeAct thismod, actionStop
	return

#modterm
	return

#modfunc playerUpdate
	if mIsActive == false : return

	playerControllAction thismod

	mIsActionChange = mIsOldActionChange;
	mIsOldActionChange = false

	playerUpdateAction thismod
	mX = limit(mX, xLimitMin, xLimitMax)

	return

#modfunc playerDraw
	if mIsActive == false : return

	chipIndex = animUpdate(mAnim, sAnimDatas)

	pos mX, mY
	color transparence
	celput mImageID, chipIndex

	return




#modfunc playerInitAnimData
	if sIsAnimDate : return
	sIsAnimDate = true

	_dataNum = 32
	_dataSize = 32
	dim _animDatas, _dataNum * _dataSize
	_animDatas(aStL*32)       = 6,16, 7,16, -1
	_animDatas(aStR*32)       = 8,16, 9,16, -1

	_animDatas(aWlL*32)       = 0,6, 1,6, 2,6, -1
	_animDatas(aWlR*32)       = 3,6, 4,6, 5,6, -1

	_animDatas(aSpReadyL*32)  = 6,-1
	_animDatas(aSpSprintL*32) = 10,2, 11,2, 12,2, -1
	_animDatas(aSpSlipL*32)   = 20,-1

	_animDatas(aSpReadyR*32)  = 8,-1
	_animDatas(aSpSprintR*32) = 13,2, 14,2, 15,2, -1
	_animDatas(aSpSlipR*32)   = 25,-1

	_animDatas(aEsJumpL*32)   = 20,4, 21,4, 22,4, 23,-1 
	_animDatas(aEsStopL*32)   = 16,-1
	_animDatas(aEsJumpR*32)   = 24,2, 25,2, 26,2, 27,-1
	_animDatas(aEsStopR*32)   = 17,-1

	_animDatas(aCrL*32)       = 16,-1
	_animDatas(aCrR*32)       = 17,-1

	_animDatas(aAiR0*32)      = 36,-1
	_animDatas(aAiR1*32)      = 35,-1
	_animDatas(aAiR2*32)      = 34,-1
	_animDatas(aAiU*32)       = 33,-1
	_animDatas(aAiL2*32)      = 32,-1
	_animDatas(aAiL1*32)      = 31,-1
	_animDatas(aAiL0*32)      = 30,-1

	dim sAnimDatas, _dataNum, _dataSize
	repeat _dataNum
		i = cnt
		repeat _dataSize
			j = cnt
			sAnimDatas(i,j) = _animDatas(i * _dataNum + j)
		loop
	loop

	return





#modfunc playerChangeAct int _action, int _actionSub, int _direction
	if mAction == _action & mActionSub == _actionSub  & mDirection == _direction{
		return
	}
	mAction = _action
	mActionSub = _actionSub
	mDirection = _direction

	// ƒAƒNƒVƒ‡ƒ“‚Ì•ÏX‚ðŽŸƒtƒŒ[ƒ€‚Ö“`‚¦‚é‚½‚ß
	// ˆê’UmIsOldActionChange‚ÉŠi”[‚·‚é
	mIsOldActionChange = true

logmes "playerChangeAct a:" + _action + " s: " + _actionSub + " d: " + _direction
	if mAction == actionStop {
		if mDirection == dirLeft:anim = aStL : else :anim = aStR
		animChange mAnim, sAnimDatas, anim
	}
	else : if mAction == actionWalk {
		if mDirection == dirLeft:anim = aWlL : else :anim = aWlR
		animChange mAnim, sAnimDatas, anim
	}
	else : if mAction == actionCreeping {
		if mDirection == dirLeft:anim = aCrL : else :anim = aCrR
		animChange mAnim, sAnimDatas, anim
	}
	
	return


#modfunc playerControllAction

	if mEscKeyCounterL : mEscKeyCounterL--
	if mEscKeyCounterR : mEscKeyCounterR--

	if mAction == actionSprint | mAction == actionEscape | mAction == actionAIM {
		return
	}

	mMoveX = 0
	mMoveY = 0

	if isKeyDown(keyUp) {
		playerChangeAct thismod, actionAIM, 0, mDirection
	}
	else:if isKeyDown(keyDown) {
		dir = mDirection
		if isKeyDown(keyLeft) : dir = dirLeft
		if isKeyDown(keyRight) : dir = dirRight
		playerChangeAct thismod, actionCreeping, 0, dir
	}
	else:if isKeyTrigger(keyLeft) & mEscKeyCounterL != 0 {
		mEscKeyCounterL = 0
		playerChangeAct thismod, actionEscape, 0, dirLeft
	}
	else:if isKeyTrigger(keyRight) & mEscKeyCounterR != 0{
		mEscKeyCounterR = 0
		playerChangeAct thismod, actionEscape, 0, dirRight
	}
	else:if isKeyDown(keyShift) & isKeyDown(keyLeft) {
		playerChangeAct thismod, actionSprint, 0, dirLeft
	}
	else:if isKeyDown(keyShift) & isKeyDown(keyRight) {
		playerChangeAct thismod, actionSprint, 0, dirRight
	}
	else:if isKeyDown(keyLeft) {
		mMoveX = -walkX
		playerChangeAct thismod, actionWalk, 0, dirLeft
	}
	else:if isKeyDown(keyRight) {
		mMoveX = walkX
		playerChangeAct thismod, actionWalk, 0, dirRight
	}
	else {
		playerChangeAct thismod, actionStop, 0, mDirection
	}

	if mAction != actionEscape {
		if isKeyTrigger(keyLeft) {
			mEscKeyCounterL = escKeyCount
		}
		else : if isKeyTrigger(keyRight) {
			mEscKeyCounterR = escKeyCount
		}
	}

	return

#const sprintX 6
#const sprintSlipX 2

#enum sprintReady = 0
#enum sprintSprint
#enum sprintSlip
#enum sprintStop

#const sprintReadyCount (10)
#const sprintSlipCount (15)
#const sprintStopCount (10)

#modfunc playerUpdateSprint

	if mActionSub != sprintSlip & mActionSub != sprintStop{
		if mActionSub == sprintSprint : asub = sprintSlip : else : asub = sprintStop
		if mDirection == dirLeft & isKeyUp(keyLeft) | isKeyUp(keyShift) {
			playerChangeAct thismod, mAction, asub, mDirection
		}
		if mDirection == dirRight & isKeyUp(keyRight) | isKeyUp(keyShift) {
			playerChangeAct thismod, mAction, asub, mDirection
		}
	}

	if mActionSub == sprintReady {
		if mIsActionChange {
			mActionCounter = sprintReadyCount
			if mDirection == dirLeft : anim=aSpReadyL : else : anim=aSpReadyR
			animChange mAnim, sAnimDatas, anim
		}
		mActionCounter--
		if mActionCounter == 0 {
			playerChangeAct thismod, actionSprint, sprintSprint, mDirection
		}
	}
	else : if mActionSub == sprintSprint {
		if mIsActionChange {
			if mDirection == dirLeft {
				animChange mAnim, sAnimDatas, aSpSprintL
				mMoveX = -sprintX
			} else {
				animChange mAnim, sAnimDatas, aSpSprintR
				mMoveX = sprintX
			}
		}
		mX += mMoveX
	}
	else : if mActionSub == sprintSlip {
		if mIsActionChange {
			mActionCounter = sprintSlipCount
			if mDirection == dirLeft {
				animChange mAnim, sAnimDatas, aSpSlipL
				mMoveX = -sprintSlipX
			} else {
				animChange mAnim, sAnimDatas, aSpSlipR
				mMoveX = sprintSlipX
			}
		}
		mActionCounter--
		if mActionCounter == 0 {
			playerChangeAct thismod, mAction, sprintStop, mDirection
		}
		mX += mMoveX
	}
	else : if mActionSub == sprintStop {
		if mIsActionChange {
			mActionCounter = sprintStopCount
		}
		mActionCounter--
		if mActionCounter == 0 {
			playerChangeAct thismod, actionStop, 0, mDirection
		}
	}

	return


#const escJumpX 3
#const escJumpY -5
#const escJumpG 1
#enum escJump = 0
#enum escStop

#const escStopCount (20)

#modfunc playerUpdateEscape
	if mActionSub == escJump {
		if mIsActionChange {
			mMoveY = escJumpY
			mActionCounter = escJumpCount
			if mDirection == dirLeft {
				animChange mAnim, sAnimDatas, aEsJumpL
				mMoveX = -escJumpX
			} else {
				animChange mAnim, sAnimDatas, aEsJumpR
				mMoveX = escJumpX
			}
		}
		mX += mMoveX
		mY += mMoveY
		mMoveY += escJumpG
		if mY >= baseY {
			mY = baseY
			playerChangeAct thismod, mAction, escStop, mDirection
		}
	}
	else : if mActionSub == escStop {
		if mIsActionChange {
			mActionCounter = escStopCount
			if mDirection == dirLeft : anim=aEsStopL : else : anim=aEsStopR
			animChange mAnim, sAnimDatas, anim
		}
		mActionCounter--
		if mActionCounter == 0 {
			playerChangeAct thismod, actionCreeping, 0, mDirection
		}
	}
	return


#const aimStopCount (60)

#modfunc playerUpdateAim
	if mIsActionChange {
		mActionCounter = 0
		mRocket = -1
		getPickupRocket thismod, mX, mY
	}

	if mActionCounter != 0 {
		mActionCounter--
		return
	}

	reticleX = getMouseX()
	reticleY = getMouseY()

	diffX = reticleX - mX
	diffY = reticleY - mY
	if diffY > 0 : diffY = 0
	angle = atan(-diffY, diffX)
	dgree = (angle / M_PI) * 180

	tmp = floor((dgree+15) / 30)
	angleIndex = limit(tmp, 0, 6)

	if vartype(mRocket) == vartype("struct") {
		rocketSetPos mRocket, (mX+14), mY
		rocketSetAngle mRocket, -angle
		if isKeyTrigger(mouseLeft) {
			rocketFire mRocket
			mActionCounter = aimStopCount
		}
	}
	animChange mAnim, sAnimDatas, aAiR0 + angleIndex

	if isKeyUp(keyUp) {
		playerChangeAct thismod, actionStop, 0, mDirection

		if vartype(mRocket) == vartype("struct") {
			if rocketIsReady(mRocket) {
				rocketRelease mRocket
			} else {
				rocketDisposal mRocket
			}
		}
	}

	return


#modfunc playerUpdateAction
	if mAction == actionAIM {
		playerUpdateAim thismod
	}
	else :if mAction == actionStop {
		
	}
	else : if mAction == actionWalk {
		mX += mMoveX
	}
	else : if mAction == actionSprint {
		playerUpdateSprint thismod
	}
	else : if mAction == actionEscape {
		playerUpdateEscape thismod
	}
	else : if mAction == actionCreeping {
		
	}

	return


#modcfunc playerGetX
	return mX

#modcfunc playerGetY
	return mY

#modcfunc playerIsDisable
	return mIsActive == false

#modcfunc playerIsActive
	return mIsActive


#modfunc playerSetRocket var _rocket
	mRocket = _rocket
	return

#modfunc playerDisable
	mIsActive = false
	return


#global


#endif
