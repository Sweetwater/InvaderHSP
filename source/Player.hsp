#ifndef ___Player___
#define ___Player___


#include "./source/Define.hsp"
#include "./source/Bullet.hsp"

#module ModPlayer mX, mY, mIsActive, mImageID

#const startX screenCenterX
#const startY 450

#const moveX 3

#const xLimitMin 0
#const xLimitMax screenWidth

#modinit int imageID
	mImageID = imageID
	mX = startX
	mY = startY
	mIsActive = true
	return

#modterm
	return

#modfunc playerUpdate array bullets
	if mIsActive == false : return

	if isKeyDown(keyLeft) {
		mX -= moveX
	}
	if isKeyDown(keyRight) {
		mX += moveX
	}
	mX = limit(mX, xLimitMin, xLimitMax)

	if isKeyTrigger(keySpace) {
		repeat length(bullets)
			if bulletIsActive(bullets(cnt)) == false {
				bulletFire bullets(cnt), mX, mY, dirUp
				break
			}
		loop
	}

	return

#modfunc playerDraw
	if mIsActive == false : return

	pos mX, mY
	celput mImageID, 0
	return




#modfunc playerDisable
	mIsActive = false
	return

#modcfunc playerGetX
	return mX

#modcfunc playerGetY
	return mY

#global


#endif
