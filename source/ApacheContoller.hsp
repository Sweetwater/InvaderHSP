#ifndef ___ApacheController___
#define ___ApacheController___

#include "./source/Define.hsp"
#include "./source/Apache.hsp"


#module ModApacheController	mApache,			\
							mTargetX, mTargetY,	\
							mIsOldPChange,		\
							mIsPChange,			\
							mPattern,			\
							mPatternSub,		\
							mPatternCounter,	\
							mLimitCounter,		\
							mState

#include "./source/ApacheDefine.hsp"

#define ctype toRadian(%1) (double(%1) * M_PI / 180)

#define ctype rnd2(%1, %2)  (rnd(%2-%1) + %1)

#const upCanonnCount (60)
#const limitCount    (360)

#enum patternEntry0 = 0
#enum patternEntry1
#enum patternFlight
#enum patternUp
#enum patternUpCannon
#enum patternDiveUp
#enum patternDiveRocket
#enum patternFastForwardCannon
#enum patternDownRocket
#enum patternLRCannon
#enum patternLRFlight


#modinit var _apache
	mApache = _apache

	if sStaticInit : return
	sStaticInit = true

	dim sLocals0, 32
	dim sLocals1, 32
	dim sLocals2, 32
	dim sLocals3, 32

	return

#modterm
	return

#modfunc aCtrUpdate
	if apacheIsDisable(mApache) : return

	aCtrController thismod

	mIsPChange = mIsOldPChange
	mIsOldPChange = false

	aX = apacheGetX(mApache)
	aY = apacheGetY(mApache)
	aD = apacheGetDirection(mApache)

	isEnd = false
	if mPattern == patternEntry0 {
		isEnd = updatePEntry0(thismod)
	}
	else : if mPattern == patternEntry1 {
		isEnd = updatePEntry1(thismod)
	}
	else : if mPattern == patternFlight {
		isEnd = updatePFlight(thismod)
	}
	else : if mPattern == patternUp {
		isEnd = updatePUp(thismod)
	}
	else : if mPattern == patternUpCannon {
		isEnd = updatePUpCannon(thismod)
	}
	else : if mPattern == patternDiveUp {
		isEnd = updatePDiveUp(thismod)
	}
	else : if mPattern == patternDiveRocket {
		isEnd = updatePDiveRocket(thismod)
	}
	else : if mPattern == patternFastForwardCannon {
		isEnd = updatePFastForwardCannon(thismod)
	}
	else : if mPattern == patternDownRocket {
		isEnd = updatePDownRocket(thismod)
	}
	else : if mPattern == patternLRCannon {
		isEnd = updatePLRCannon(thismod)
	}
	else : if mPattern == patternLRFlight {
		isEnd = updatePLRFlight(thismod)
	}

	if aX < 0 | aX > screenWidth | aY < 0 | aY > screenHeight {
		mLimitCounter--
	}
	if mLimitCounter == 0 {
		aCtrRespawn thismod
	}
	else : if isEnd {
		if aD == dirLeft & aX < screenWidth/2 {
			apacheSetDirection mApache, dirRight
		}
		if aD == dirRight & aX > screenWidth/2 {
			apacheSetDirection mApache, dirLeft
		}

		sLocals0(0*2) = 20, patternFlight
		sLocals0(1*2) = 10, patternUp
		sLocals0(2*2) = 10, patternUpCannon
		sLocals0(3*2) = 10, patternDiveUp
		sLocals0(4*2) = 10, patternDiveRocket
		sLocals0(5*2) = 10, patternFastForwardCannon
		sLocals0(6*2) = 15, patternDownRocket
		sLocals0(7*2) =  5, patternLRCannon
		sLocals0(8*2) = 10, patternLRFlight

		pattern = aCtrOdds(thismod, sLocals0)
		aCtrChangePattern thismod, pattern
	}

	return



#modcfunc aCtrOdds array _odds
	rand = rnd(100)
	index = 0
	value = 0
	repeat length(_odds) / 2
		value += _odds(index*2)
		if rand < value : break
		index++
	loop

	return _odds(index*2+1)

#modcfunc aCtrCheckX int _X, int _targetX
	direction = apacheGetDirection(mApache)
	if direction == dirLeft {
		return _x < _targetX
	}
	else {
		return _x > _targetX
	}

	return 0

#modcfunc aCtrTargetX int _targetX
	direction = apacheGetDirection(mApache)
	if direction == dirLeft {
		return screenWidth - _targetX
	}
	else {
		return _targetX
	}

	return 0




#modcfunc updatePEntry0
	if aY > mTargetY {
		return true
	}
	return false

#modcfunc updatePEntry1
	if aCtrCheckX(thismod, aX, mTargetX) {
		return true
	}
	return false

#modcfunc updatePFlight
	if mIsPChange {
		apacheSetAttack mApache, attackNpne
		apacheSetAction mApache, actionFlight
		mPatternCounter = rnd2(60, 120)
	}
	mPatternCounter--
	if mPatternCounter == 0 {
		return true
	}
	return false

#modcfunc updatePUp
	if mIsPChange {
		apacheSetAttack mApache, attackNpne
		apacheSetAction mApache, actionUp
		mTargetY = rnd2(40, 60)
	}
	if aY < mTargetY {
		return true
	}
	return false

#modcfunc updatePUpCannon
	if mIsPChange{
		apacheSetAttack mApache, attackCRake
		apacheSetAction mApache, actionUp
		mPatternCounter = upCanonnCount
	}
	mPatternCounter--
	if mPatternCounter == 0 {
		return true
	}
	return false

#modcfunc updatePDiveUp
	if mPatternSub == 0 {
		if mIsPChange {
			apacheSetAttack mApache, attackNone
			apacheSetAction mApache, actionDive
			mTargetY = rnd2(200, 240)
		}
		if aY > mTargetY {
			aCtrChangePattern thismod, patternDiveUp, 1
		}
	}
	else {
		if mIsPChange {
			apacheSetAction mApache, actionUp
			mTargetY = rnd2(40, 60)
		}
		if aY < mTargetY {
			return true
		}
	}
	return false

#modcfunc updatePDiveRocket
	if mPatternSub == 0 {
		if mIsPChange {
			apacheSetAttack mApache, attackNone
			apacheSetAction mApache, actionDive
			mPatternCounter = rnd2(10, 20)
		}
		mPatternCounter--
		if mPatternCounter == 0 {
			aCtrChangePattern thismod, patternDiveRocket, 1
		}
	}
	else : if mPatternSub == 1 {
		if mIsPChange {
			apacheSetAttack mApache, attackRocket
			mTargetY = rnd2(200, 240)
		}
		if aY > mTargetY {
			aCtrChangePattern thismod, patternDiveRocket, 2
		}
	}
	else : if mPatternSub == 2 {
		if mIsPChange {
			apacheSetAction mApache, actionFastForward
		}
		if aX < 0 & aX > 640 {
			return true
		}
	}
	return false


#modcfunc updatePFastForwardCannon
	if mPatternSub == 0 {
		if mIsPChange {
			apacheSetAttack mApache, attackCTarget
			apacheSetCAngle mApache, toRadian(30)
			apacheSetAction mApache, actionFastForward
		}
		if aX < 0 & aX > 640 {
			return true
		}
	}
	return false

#modcfunc updatePDownRocket
	if mPatternSub == 0 {
		if mIsPChange {
			apacheSetAttack mApache, attackRocket
			apacheSetAction mApache, actionDown
			mTargetY = rnd2(300, 340)
		}
		if aY > mTargetY {
			return true
		}
	}
	return false


#modcfunc updatePLRCannon
	if mPatternSub == 0 {
		if mIsPChange {
			apacheSetAttack mApache, attackCRake
			apacheSetAction mApache, actionForward
			mPatternCounter = rnd2(1, 4)
			mTargetX = rnd2(540, 600)
			mTargetX = aCtrTargetX(thismod, mTargetX)
		}
		if aCtrCheckX(thismod, aX, mTargetX) {
			aCtrChangePattern thismod, patternLRCannon, 1
		}
	}
	else : if mPatternSub == 1 {
		if mIsPChange {
			if aD == dirLeft : dir=dirRight : else : dir=dirLeft
			apacheSetAction mApache, actionDown
			apacheSetDirection mApache, dir
			mTargetY = rnd2(100, 160)
		}
		if aY > mTargetY {
			aCtrChangePattern thismod, patternLRCannon, 2
		}
	}
	else : if mPatternSub == 2 {
		if mIsPChange {
			apacheSetAction mApache, actionForward
			mTargetX = rnd2(540, 600)
			mTargetX = aCtrTargetX(thismod, mTargetX)
		}
		if aCtrCheckX(thismod, aX, mTargetX) {
			aCtrChangePattern thismod, patternLRCannon, 1

			mPatternCounter--
			if mPatternCounter == 0{
				return true
			}
		}
	}
	return false

#modcfunc updatePLRFlight
	if mPatternSub == 0 {
		if mIsPChange {
			apacheSetAttack mApache, attackNone
			apacheSetAction mApache, actionForward
			mPatternCounter = rnd2(1, 3)
			mTargetX = rnd2(540, 600)
			mTargetX = aCtrTargetX(thismod, mTargetX)
		}
		if aCtrCheckX(thismod, aX, mTargetX) {
			aCtrChangePattern thismod, patternLRFlight, 1
		}
	}
	else : if mPatternSub == 1 {
		if mIsPChange {
			if aD == dirLeft : dir=dirRight : else : dir=dirLeft
			apacheSetAction mApache, actionDown
			apacheSetDirection mApache, dir
			mTargetY = rnd2(100, 160)
		}
		if aY > mTargetY {
			aCtrChangePattern thismod, patternLRFlight, 2
		}
	}
	else : if mPatternSub == 2 {
		if mIsPChange {
			apacheSetAction mApache, actionForward
			mTargetX = rnd2(540, 600)
			mTargetX = aCtrTargetX(thismod, mTargetX)
		}
		if aCtrCheckX(thismod, aX, mTargetX) {
			aCtrChangePattern thismod, patternLRFlight, 1

			mPatternCounter--
			if mPatternCounter == 0{
				return true
			}
		}
	}
	return false





#modfunc aCtrChangePattern int _pattern, int _patternSub
	mPattern = _pattern
	mPatternSub = _patternSub
	mIsOldPChange = true

// logmes "changePattern :" + mPattern
// logmes "sub           :" + mPatternSub

	return







#modfunc aCtrRespawn
	mLimitCounter = limitCount

	resType = rnd(3)
	if resType == 0 {
		y = rnd2(-130, -30)
		x = rnd2(0, 640)
		mTargetY = rnd2(100, 200)

		direction = dirRight
		if x > 320 : direction = dirLeft

		sLocals0 = 95,actionDown, 5,actionDive

		apacheRespawn mApache, x, y, direction
		apacheSetAction mApache, aCtrOdds(thismod, sLocals0)
		mPattern = patternEntry0
	}
	else : if resType == 1 {
		y = rnd2(100, 300)
		x = rnd2(-50, 0)
		mTargetX = rnd2(60, 600)
		sLocals0 = 90,actionForward, 10,actionFastForward

		apacheRespawn mApache, x, y, dirRight
		apacheSetAction mApache, aCtrOdds(thismod, sLocals0)
		mPattern = patternEntry1
	}
	else : if resType == 2 {
		y = rnd2(100, 300)
		x = rnd2(640, 690)
		mTargetX = rnd2(40, 580)
		sLocals0 = 90,actionForward, 10,actionFastForward

		apacheRespawn mApache, x, y, dirLeft
		apacheSetAction mApache, aCtrOdds(thismod, sLocals0)
		mPattern = patternEntry1
	}

	apacheSetCAngle mApache, toRadian(30)
	sLocals0 = 10,attackNone, 60,attackCTarget, 30,attackRocket
	apacheSetAttack mApache, aCtrOdds(thismod, sLocals0)
/*
logmes "respawn   : " + resType
logmes "x         : " + x
logmes "y         : " + y
logmes "targetX   : " + mTargetX
logmes "targetY   : " + mTargetY
logmes "action    : " + apacheGetAction(mApache)
logmes "direction : " + apacheGetDirection(mApache)
*/
	return





#modfunc aCtrController
	if isKeyTrigger(keyD0):aCtrChangePattern thismod, patternLRCannon
//	if isKeyTrigger(keyD1):aCtrChangePattern thismod, 4
//	if isKeyTrigger(keyD2):aCtrChangePattern thismod, 5
//	if isKeyTrigger(keyD3):aCtrChangePattern thismod, 6
//	if isKeyTrigger(keyD4):aCtrChangePattern thismod, 7
//	if isKeyTrigger(keyD5):aCtrChangePattern thismod, 8
//	if isKeyTrigger(keyD6):aCtrChangePattern thismod, 9
//	if isKeyTrigger(keyD7):aCtrChangePattern thismod, 10
//	if isKeyTrigger(keyD8):aCtrChangePattern thismod, 11
//	if isKeyTrigger(keyD9):aCtrChangePattern thismod, 12
	return

#global


#endif
